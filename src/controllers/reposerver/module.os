#Использовать "../../model"
#Использовать crs-api

Перем ЗаголовокСписка;
Перем ЗаголовокЗаписи;

#Область ПрограммыйИнтерфейс

Функция Index() Экспорт
	
	Модель = ОбщегоНазначения.МодельСтраницы(ЗаголовокСписка);
	Модель.Вставить("Список", МенеджерБазДанных.СписокСерверовХранилищ());
	Возврат Представление("page", Модель);
	
КонецФункции

Функция Save() Экспорт
	
	Запись = Неопределено;
	
	Если ЭтотОбъект.ЗапросHTTP.Метод = "POST" Тогда
		
		ИдентификаторЗаписи = ЭтотОбъект.ЗапросHTTP.ДанныеФормы["Идентификатор"];
		Если ЗначениеЗаполнено(ИдентификаторЗаписи) Тогда		
			Запись = МенеджерБазДанных.СерверХранилищПоИдентификатору(ИдентификаторЗаписи);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Запись) Тогда
			Запись = Новый СерверХранилищ();
		КонецЕсли;
		
		Запись.Идентификатор   = ИдентификаторЗаписи;
		Запись.Имя             = ЭтотОбъект.ЗапросHTTP.ДанныеФормы["Имя"];
		Запись.Адрес           = ЭтотОбъект.ЗапросHTTP.ДанныеФормы["Адрес"];
		Запись.ВерсияПлатформы = ЭтотОбъект.ЗапросHTTP.ДанныеФормы["ВерсияПлатформы"];
		Запись.Комментарий     = ЭтотОбъект.ЗапросHTTP.ДанныеФормы["Комментарий"];
		
		МенеджерБазДанных.МенеджерСущностей.Сохранить(Запись);
		
		Возврат Перенаправление("/reposerver");
		
	КонецЕсли;
	
	Модель = ОбщегоНазначения.МодельСтраницы(ЗаголовокЗаписи);
	Модель.Вставить("Запись", "Сервер хранилищ конфигураций"); // fixme: это заголовок записи
	
	Возврат Index();
	
КонецФункции

Функция Add() Экспорт
	
	Запись = ШаблонЗаписи();
	Модель = ОбщегоНазначения.МодельСтраницы(ЗаголовокЗаписи);
	Модель.Вставить("Запись", Запись);
	Модель.Вставить("ЗаголовокЗаписи", "Новая запись");
	Возврат Представление("element", Модель);
	
КонецФункции

Функция Edit() Экспорт
	
	Запись = ШаблонЗаписи();
	
	Если ЭтотОбъект.ЗапросHTTP.Метод = "POST" Тогда
		ИдентификаторЗаписи = ЭтотОбъект.ЗапросHTTP.ДанныеФормы["Идентификатор"];
		СуществующаяЗапись = МенеджерБазДанных.СерверХранилищПоИдентификатору(ИдентификаторЗаписи);
		ЗаполнитьЗначенияСвойств(Запись, СуществующаяЗапись);
	КонецЕсли;
	
	Модель = ОбщегоНазначения.МодельСтраницы(ЗаголовокЗаписи);
	Модель.Вставить("Запись", Запись);
	Модель.Вставить("ЗаголовокЗаписи", "Существующая запись");
	
	Возврат Представление("element", Модель);
	
КонецФункции

Функция Remove() Экспорт
	
	Если ЭтотОбъект.ЗапросHTTP.Метод = "POST" Тогда
		ИдентификаторЗаписи = ЭтотОбъект.ЗапросHTTP.ДанныеФормы["Идентификатор"];
		Если ЗначениеЗаполнено(ИдентификаторЗаписи) Тогда
			СуществующаяЗапись = МенеджерБазДанных.СерверХранилищПоИдентификатору(ИдентификаторЗаписи);
			Если СуществующаяЗапись <> Неопределено Тогда
				МенеджерБазДанных.МенеджерСущностей.Удалить(СуществующаяЗапись);
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;
	
	Возврат Index();
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыФункции

Функция ШаблонЗаписи()
	
	Шаблон = Новый Структура;
	
	Шаблон.Вставить("Имя");
	Шаблон.Вставить("Идентификатор");
	Шаблон.Вставить("Адрес");
	Шаблон.Вставить("ВерсияПлатформы");
	Шаблон.Вставить("Комментарий");
	
	Возврат Шаблон;
	
КонецФункции

#КонецОбласти

ЗаголовокСписка = "Зарегистрированные сервера хранилищ конфигураций";
ЗаголовокЗаписи = "Сервер хранилищ конфигураций";