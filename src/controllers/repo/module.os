#Использовать "../../model"
#Использовать crs-api

Перем ЗаголовокСписка;
Перем ЗаголовокЗаписи;

#Область ПрограммыйИнтерфейс

Функция Index() Экспорт
	
	Модель = ОбщегоНазначения.МодельСтраницы(ЗаголовокСписка);
	Модель.Вставить("Список", МенеджерБазДанных.СписокХранилищКонфигураций());
	Возврат Представление("page", Модель);
	
КонецФункции

Функция Save() Экспорт
	
	Запись = Неопределено;
	
	Если ЭтотОбъект.ЗапросHTTP.Метод = "POST" Тогда
		
		ИдентификаторЗаписи = ЭтотОбъект.ЗапросHTTP.ДанныеФормы["Идентификатор"];
		Если ЗначениеЗаполнено(ИдентификаторЗаписи) Тогда		
			Запись = МенеджерБазДанных.ХранилищеПоИдентификатору(ИдентификаторЗаписи);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Запись) Тогда
			Запись = Новый ХранилищеКонфигурации();
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ЭтотОбъект.ЗапросHTTP.ДанныеФормы["Пароль"]) Тогда
			Пароль = ЭтотОбъект.ЗапросHTTP.ДанныеФормы["Пароль"];
		Иначе
			Пароль = Запись.Пароль;
		КонецЕсли;
		
		Запись.Идентификатор   = ИдентификаторЗаписи;
		Запись.Имя             = ЭтотОбъект.ЗапросHTTP.ДанныеФормы["Имя"];
		Запись.Ссылка          = ЭтотОбъект.ЗапросHTTP.ДанныеФормы["Ссылка"];
		Запись.ВерсияПлатформы = ЭтотОбъект.ЗапросHTTP.ДанныеФормы["ВерсияПлатформы"];
		Запись.Пользователь    = ЭтотОбъект.ЗапросHTTP.ДанныеФормы["Пользователь"];
		Запись.Пароль          = Пароль;
		Запись.Комментарий     = ЭтотОбъект.ЗапросHTTP.ДанныеФормы["Комментарий"];
		
		МенеджерБазДанных.МенеджерСущностей.Сохранить(Запись);
		
		Возврат Перенаправление("/repo");
		
	КонецЕсли;
	
	Модель = ОбщегоНазначения.МодельСтраницы(ЗаголовокЗаписи);
	Модель.Вставить("Запись", "Хранилище конфигурации"); // fixme: это заголовок записи
	
	Возврат Index();
	
КонецФункции

Функция Add() Экспорт
	
	Запись = ШаблонЗаписи();
	Модель = ОбщегоНазначения.МодельСтраницы(ЗаголовокЗаписи);
	Модель.Вставить("Запись", Запись);
	Модель.Вставить("ЗаголовокЗаписи", "Новая запись");
	Возврат Представление("element", Модель);
	
КонецФункции

Функция Edit() Экспорт
	
	Запись = ШаблонЗаписи();
	
	Если ЭтотОбъект.ЗапросHTTP.Метод = "POST" Тогда
		ИдентификаторЗаписи = ЭтотОбъект.ЗапросHTTP.ДанныеФормы["Идентификатор"];
		СуществующаяЗапись = МенеджерБазДанных.ХранилищеПоИдентификатору(ИдентификаторЗаписи);
		ЗаполнитьЗначенияСвойств(Запись, СуществующаяЗапись);
	КонецЕсли;
	
	Модель = ОбщегоНазначения.МодельСтраницы(ЗаголовокЗаписи);
	Модель.Вставить("Запись", Запись);
	Модель.Вставить("ЗаголовокЗаписи", "Существующая запись");
	
	Возврат Представление("element", Модель);
	
КонецФункции

Функция Remove() Экспорт
	
	Если ЭтотОбъект.ЗапросHTTP.Метод = "POST" Тогда
		ИдентификаторЗаписи = ЭтотОбъект.ЗапросHTTP.ДанныеФормы["Идентификатор"]; // ???
		Если ЗначениеЗаполнено(ИдентификаторЗаписи) Тогда
			СуществующаяЗапись = МенеджерБазДанных.ХранилищеПоИдентификатору(ИдентификаторЗаписи);
			Если СуществующаяЗапись <> Неопределено Тогда
				МенеджерБазДанных.МенеджерСущностей.Удалить(СуществующаяЗапись);
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;
	
	Возврат Index();
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыФункции

Функция ШаблонЗаписи()
	
	Шаблон = Новый Структура;
	
	Шаблон.Вставить("Имя");
	Шаблон.Вставить("Идентификатор");
	Шаблон.Вставить("Ссылка");
	Шаблон.Вставить("ВерсияПлатформы");
	Шаблон.Вставить("Пользователь");
	Шаблон.Вставить("ХешПароля");
	Шаблон.Вставить("Комментарий");
	
	Возврат Шаблон;
	
КонецФункции

#КонецОбласти

ЗаголовокСписка = "Список хранилищ конфигураций";
ЗаголовокЗаписи = "Хранилище конфигурации";